# Little Man Computer Program to convert integer n between base a and base b

# Brief Overview of algorithm #
# 1) Take in inputs n, a, b
# 2) Get digits from n
# 3) Multiply most significant digit by a**2, second by a, third by 1
# 4) Add products together.
# 5) Take the sum and divide by b**2. Remainder then divided by b.
# 6) Take first integer division result and multiply by 100. Then take second and multiply by 10. Sum up the products and the final remainder.
# 7) Output the final sum.

# Possible sources of errors #
# - Overflow in addition operation
# - A base larger than 10 or smaller than 2 being entered
# - A n that results in an output that cannot be display in 3 digits

# Further Detail on critical aspects
# - Branch returns:
# -- By loading current memory location and then adding 104 (to point to return point and also to offset LDA instruction opcode) and storing this result in DAT at the end of a 'function' the program will jump back to just after where it was called allowing for 'functions' to be reused to save on lines.

# To do:
# - Deal with errors detailed before
# -- Impossible with number of letterboxes remaining. Complete next ToDo first.
# - Reduce number of letterboxes
# -- Possible way is to loop similar structures such as mul/div blocks called in main
# - Move status variable check if number of letterboxes allows for it in order to reduce number of fetch execute cycles.

# Data dictionary
# Variables names detailed at end of program.

# CODE BEGINS HERE #
	LDA	010 # Load default values for certain variables to allow for restart without recompiling
	STO	ebase
	LDA	100
	STO	divval
	LDA	001
	STO	status

	IN # Take in n, a, b as inputs
	STO	n
	IN
	BRZ	err
	STO	a
	IN
	STO	b
	
start	LDA	a 
	STO	mulval
	STO	count
r1	LDA	r1
	BR	mul # squares sbase

r2	LDA	r2
	BR	div # divides n by ebase**2, count becomes d1

	LDA	temp
	STO	mulval # sets mulvalue to sbase**2
r3	LDA	r3
	BR	mul # d1 * base ** 2

	STO	temp2 # stores d1 * base ** 2 in temporary storage
	LDA	ebase # load non square base
	STO	divval
r4	LDA	r4
	BR	div # divide and get d2
	
	LDA	a
	STO	mulval
r5	LDA	r5
	BR	mul # multiply d2 * base

	ADD	temp2 # add d1 * base ** 2
	ADD	n # add d3
	STO	n
	
# Move status check here to save cycles #

	LDA	ebase
	STO	a # Swap start and end bases
	LDA	b
	STO	ebase
	STO	count # Load mulval and count for squaring
	STO	mulval

r6	LDA	r6
	BR	mul # Square the base

	STO	divval # Pre-emptively store square of base as division value
	LDA	status # Load status variable
	SUB	001
	STO	status
	BRP	start # If status negative the end
	
	LDA	n
out	OUT # Output n
001	DAT	001 # HLT

# INT DIV /w REMAINDER #
div	ADD	102
	STO	eodiv
	LDA	000
	STO	count # Reset count
divst	LDA	n # Load running total
	SUB	divval # Subtract divval from n
	BRP	divcon # If negative then exit
eodiv	DAT	000 # Return to branch point
divcon	STO	n # Update stored n
	LDA	count 
	ADD	001 # Increment count
	STO	count
	SUB	a
	BRZ	err
	BR	divst # Loop

# INT MULT #
mul	ADD	102
	STO	eomul
	LDA	000 
	STO	temp # Reset temp value
mulst	LDA	count # Load count
	BRZ	_eomul # If count is zero exit
	SUB	001 
	STO	count # Decrement count
	LDA	temp 
	ADD	mulval
	STO	temp
	SUB	mulval
	BRP	overfg
err	LDA	999
	BR	out
overfg	BR	mulst # Loop
_eomul	LDA	temp # Load temp into accumulator (saves lines later)
eomul	DAT	000 # Return to branch point

# Variable list
n	DAT 		# Stores initial value, div remainder and final output
a	DAT 		# Stores starting base and later becomes a psuedo 'sbase' (to the ebase)
b	DAT 		# Stores final ending base.
divval	DAT 		# Stores value to subtract each loop during division
100	DAT	100 	# Simply stores value 100
010	DAT	010 	# Simply stores value 010
mulval	DAT		# Stores value to add each loop during multiplication
ebase	DAT		# End base, current target base to convert to.
102	DAT	102 	# Branch return point offset
000	DAT	000 	# Simply stores value 000
temp	DAT		# A temporary data location.
temp2	DAT 		# A second temporary data location. Could perhaps be replaced by n?
count	DAT 		# Integer result of division operation and also number of loops remaining during multiplication operation.
status	DAT	001 	# Stores number of master loops + 1 to run.
999	DAT	999